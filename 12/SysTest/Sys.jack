// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {
    static int coefficient;

    /** Performs all the initializations required by the OS. */
    function void init() {
        do Memory.init(); // Array
        do Keyboard.init(); // none
        do Math.init();     // Array 
        do Output.init(); // none
        do Screen.init(); // none  
        let coefficient = 10;
        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while(true) {
            // loop forever
        }
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int i;
        let i = duration * coefficient;
        while (i > 0) {
            let i = i - 1;            
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        do Output.moveCursor(0, 0);
        do Output.printString("ERR ");
        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }
}
